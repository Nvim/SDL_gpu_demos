#version 450

#extension GL_GOOGLE_include_directive : require
#include "grass_instance.glsl"
#include "grass_gen.h"

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout(std140, set = 2, binding = 0) uniform uSettings {
    GrassGenerationParams params;
};

layout(set = 1, binding = 0) writeonly buffer InstanceBuffer {
    GrassInstance Instances[];
};

float rand(float x) {
    return fract(sin(x) * 100000.f);
}

#define FLAG_ON(value) bool(params.flags & value)
const float PI = 3.14159265358979323846;

void main() {
    uint idx = gl_GlobalInvocationID.x + (gl_GlobalInvocationID.y * params.grass_per_chunk*params.chunk_count);
    Instances[idx].color = params.base_color.xyz;

    Instances[idx].rotation = FLAG_ON(GRASS_ROTATE) ? rand(idx) * PI : 0.f;

    float off_x = 0.f;
    float off_z = 0.f;
    if (FLAG_ON(GRASS_OFFSET_POS)) {
        off_x = mod(rand(idx) * 2 - 1, params.offset_cap);
        off_z = mod(rand(off_x) * 2 - 1, params.offset_cap);
    }

    Instances[idx].world_pos = vec3((gl_GlobalInvocationID.x / params.density) + off_x,
            0.f, (gl_GlobalInvocationID.y / params.density) + off_z);
}
